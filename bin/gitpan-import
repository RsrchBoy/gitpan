#!/usr/bin/perl

use Gitpan::perl5i;

use Getopt::Long;
use Gitpan;

# Defaults
my %opts = (
    workers        => 3,
    overwrite_repo => 0,
);

# These are options which change what distributions are
# imported.
my @search_opts = qw(
    author
    random
    name_like
    since
    limit
);

GetOptions(
    \%opts,
    "author=s",
    "random=i",
    "name_like=s",
    "since=s",
    "sort!",
    "limit=i",
    "workers=i",
    "overwrite_repo!",
);

my $gitpan = Gitpan->new;

if( @ARGV ) {
    $gitpan->import_from_distnames(
        \@ARGV,
        num_workers    => $opts{workers},
        overwrite_repo => $opts{overwrite_repo}
    );
}
else {
    my $bp_dists = $gitpan->select_dists(
        limit           => $opts{limit},
        name_like       => $opts{name_like},
        author          => $opts{author},
        sort            => $opts{sort},
        random          => $opts{random},
        since           => $opts{since},
    );
    $gitpan->import_from_backpan_dists(
        bp_dists        => $bp_dists,
        all_dists       => !_has_search_opts(\%opts),
        num_workers     => $opts{workers},
        overwrite_repo  => $opts{overwrite_repo},
    );
}


func _has_search_opts( HashRef $opts ) {
    for my $search_opt (@search_opts) {
        return 1 if exists $opts->{$search_opt};
    }

    return 0;
}

__END__

=head1 NAME

gitpan - Import all of BackPAN to github

=head1 SYNOPSIS

    gitpan [options] [dist] [dist] ...

=head1 DESCRIPTION

gitpan is a script to import large chunks (or all of) BackPAN into git
and github.  It allows you to select BackPAN distributions to import
and control importing to github.

=head1 OPTIONS

=head2 Distribution Selection

By default, gitpan will import every distribution on BackPAN.  You can
override this by passing it a list of distributions on the command
line or the following.

=head3 --author

Import all the options of a particular author.  Give it their CPAN id.
For example

    gitpan --author=MSCHWERN

=head3 --random

Pick N random distributions.

    gitpan --random=100

=head3 --name_list

Pick all distributions matching the given SQL LIKE pattern.

    # Import all the D's.
    gitpan --name_like='D%'

=head3 --sort

Sort the distributions by name before processing

=head3 --since

Pick only distributions which have changed since the given time.

Time is given in Unix epoch time.

    gitpan --since=1200000000

=head3 --limit

Limit how many distributions are imported

    gitpan --limit=5


=head2 Gitpan Details

=head3 --workers

Specify the number of worker processes to use.

Default is 3.

=head3 --overwrite_repo

Delete the local and Github repo before importing.  Use this if you
want to reimport the whole distribution.

Default is false.


=head1 EXAMPLE

Here is how gitpan does its importing.

    gitpan --name_like='F%' --sort

Import all the distributions starting with "F" in name order.

=cut
