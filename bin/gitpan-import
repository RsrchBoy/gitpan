#!/usr/bin/perl

use Gitpan::perl5i;

use Getopt::Long;
use Gitpan;

# Defaults
my %opts = (
    workers     => 3,
    delete_repo => 0,
);

GetOptions(
    \%opts,
    "author=s",
    "random=i",
    "name_like=s",
    "sort!",
    "since=s",
    "limit=i",
    "workers=i",
    "delete_repo!",
);

my $gitpan = Gitpan->new;

my @dists;
if( @ARGV ) {
    $gitpan->import_from_distnames(\@ARGV, delete_repo => $opts{delete_repo});
}
else {
    my $dists = $gitpan->backpan_index->dists;

    if( $opts{since} ) {
        $dists = $dists->search( latest_date => \">= $opts{since}" );
    }

    if( $opts{random} ) {
        my $num = $opts{random};

        $dists = $dists->search( undef, { order_by => \'random()', rows => $num } );
    }
    elsif( $opts{sort} ) {
        $dists = $dists->search( undef, { order_by => 'name' } );
    }

    if( $opts{author} ) {
        $dists = $dists->search({ "releases.cpanid" => $opts{author} },
                                { join => "releases", distinct => 1 });
    }

    if( $opts{name_like} ) {
        $dists = $dists->search({ name => { -like => $opts{name_like} } });
    }

    if( $opts{limit} ) {
        $dists = $dists->search( undef, { rows => $opts{limit} });
    }

    $gitpan->import_from_backpan_dists(
        $dists,
        num_workers => $opts{workers},
        delete_repo => $opts{delete_repo},
    );
}


__END__

=head1 NAME

gitpan - Import all of BackPAN to github

=head1 SYNOPSIS

    gitpan [options] [dist] [dist] ...

=head1 DESCRIPTION

gitpan is a script to import large chunks (or all of) BackPAN into git
and github.  It allows you to select BackPAN distributions to import
and control importing to github.

=head1 OPTIONS

=head2 Distribution Selection

By default, gitpan will import every distribution on BackPAN.  You can
override this by passing it a list of distributions on the command
line or the following.

=head3 --author

Import all the options of a particular author.  Give it their CPAN id.
For example

    gitpan --author=MSCHWERN

=head3 --random

Pick N random distributions.

    gitpan --random=100

=head3 --name_list

Pick all distributions matching the given SQL LIKE pattern.

    # Import all the D's.
    gitpan --name_like='D%'

=head3 --sort

Sort the distributions by name before processing

=head3 --since

Pick only distributions which have changed since the given time.

Time is given in Unix epoch time.

    gitpan --since=1200000000

=head3 --limit

Limit how many distributions are imported

    gitpan --limit=5


=head2 Gitpan Details

=head3 --workers

Specify the number of worker processes to use.

Default is 3.

=head3 --delete_repo

Delete the local and Github repo before importing.  Use this if you
want to reimport the whole distribution.

Default is false.


=head1 EXAMPLE

Here is how gitpan does its importing.

    gitpan --name_like='F%' --sort

Import all the distributions starting with "F" in name order.

=cut
